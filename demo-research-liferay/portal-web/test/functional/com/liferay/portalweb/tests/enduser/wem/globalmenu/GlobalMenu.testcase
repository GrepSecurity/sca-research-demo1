@component-name = "portal-wcm"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Control Menu";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			Site.tearDownCP();
		}
	}

	@description = "This is a use case for LPS-112990. The Global Menu icon and User Avatar icon buttons are located in Control Menu when user is in application admin. The Global Menu icon is located in Control Menu but User Avatar icon is located in Nav Bar when user is in site page. The Global Menu icon is not shown but User Avatar icon is located in Nav Bar when user is in page editor. The Global Menu icon is located in Control Menu but User Avatar icon is located in Nav Bar when user is in Staging site page and Live site page."
	@priority = "5"
	test AssertLocationOfGlobalMenuIconAndUserAvatarIcon {
		task ("The Global Menu icon is located in Control Menu but User Avatar icon is located in Nav Bar when user is in site page") {
			Navigator.gotoPage(pageName = "Home");

			AssertElementPresent(locator1 = "ControlMenu#GLOBAL_MENU_ICON");

			AssertElementPresent(locator1 = "UserBar#USER_AVATAR_IN_NAVIGATION_USER_PERSONAL_BAR_PORTLET");
		}

		task ("The Global Menu icon and User Avatar icon buttons are located in Control Menu when user is in application admin") {
			SitePages.openPagesAdmin(siteURLKey = "guest");

			MetalComponent.waitForManagementBar();

			AssertElementPresent(locator1 = "ControlMenu#GLOBAL_MENU_ICON");

			AssertElementPresent(locator1 = "UserBar#USER_AVATAR_IN_CONTROL_MENU");

			Site.openSitesAdmin();

			MetalComponent.waitForManagementBar();

			AssertElementPresent(locator1 = "ControlMenu#GLOBAL_MENU_ICON");

			AssertElementPresent(locator1 = "UserBar#USER_AVATAR_IN_CONTROL_MENU");
		}

		task ("The Global Menu icon is not shown but User Avatar icon is located in Nav Bar when user is in page editor") {
			JSONLayout.addPublicLayout(
				groupName = "Guest",
				layoutName = "Test Content Page Name",
				type = "content");

			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Content Page Name",
				siteName = "Guest");

			AssertElementNotPresent(locator1 = "GlobalMenu#TOGGLE");

			AssertElementPresent(locator1 = "UserBar#USER_AVATAR_IN_NAVIGATION_USER_PERSONAL_BAR_PORTLET");
		}

		task ("The Global Menu icon is located in Control Menu but User Avatar icon is located in Nav Bar when user is in Staging site page and Live site page.") {
			JSONGroup.addGroup(groupName = "Test Site Name");

			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Page Name");

			Staging.openStagingAdmin(siteURLKey = "test-site-name");

			Staging.activateStagingCP(siteName = "Test Site Name");

			Navigator.gotoStagedSitePage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			AssertElementPresent(locator1 = "ControlMenu#GLOBAL_MENU_ICON");

			AssertElementPresent(locator1 = "UserBar#USER_AVATAR_IN_NAVIGATION_USER_PERSONAL_BAR_PORTLET");

			Navigator.gotoSitePage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			AssertElementPresent(locator1 = "ControlMenu#GLOBAL_MENU_ICON");

			AssertElementPresent(locator1 = "UserBar#USER_AVATAR_IN_NAVIGATION_USER_PERSONAL_BAR_PORTLET");
		}
	}

	@description = "This is a use case for LPS-112989. View All link will be shown in Global Menu when the total amount of Recently Visited and My Sites more than 7."
	@priority = "5"
	test ShowViewAllWhenTotalAmountOfSitesFromRecentlyVisitedAndMySitesMoreThanSeven {
		property portal.acceptance = "true";
		property testray.component.names = "Web Content Administration";

		task ("Add six new sites") {
			for (var n : list "1,2,3,4,5,6") {
				JSONGroup.addGroup(groupName = "Test Site Name ${n}");
			}
		}

		task ("Assert View All link shown in Global Menu") {
			GlobalMenuHelper.openGlobalMenu();

			AssertVisible(locator1 = "GlobalMenu#VIEW_ALL_LINK");
		}

		task ("Go to Site via View All link") {
			WaitForLiferayEvent.initializeLiferayEventLog();

			Click(locator1 = "GlobalMenu#VIEW_ALL_LINK");

			AssertVisible(locator1 = "Portlet#MODAL_TITLE");

			SelectFrame(locator1 = "IFrame#MODAL_BODY");

			Navigator.gotoNavItem(navItem = "My Sites");

			SiteSelector.selectSite(site = "Test Site Name 3");

			ProductMenuHelper.openProductMenu();

			AssertTextEquals.assertPartialText(
				locator1 = "ProductMenu#PRODUCT_MENU_PANEL_SITE_ADMINISTRATION_SITE_NAME",
				value1 = "Test Site Name 3");
		}
	}

}